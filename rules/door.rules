import org.openhab.core.library.types.*

rule Startup
when
	System started
then
	logInfo("chicken", "system started")
	postUpdate(OPS_MODE, 1)
end

rule ModeChange
when
    Item OPS_MODE received update
then
	logInfo("chicken", "mode changed")
    if (OPS_MODE.state == 0) { // closed
		logInfo("chicken", "manually closing door")
        sendCommand(RELAY_1, OFF)
        sendCommand(RELAY_2, OFF)
    }
    if (OPS_MODE.state == 1) {
    	logInfo("chicken", "set door to auto mode")
		if (now.isAfter((SUNRISE_TIME.state as DateTimeType).calendar.timeInMillis) && now.isBefore((SUNSET_TIME.state as DateTimeType).calendar.timeInMillis)) {
	        sendCommand(RELAY_1, ON)
	        sendCommand(RELAY_2, ON)
		} else {
	        sendCommand(RELAY_1, OFF)
	        sendCommand(RELAY_2, OFF)
		}
    }
    if (OPS_MODE.state == 2) {
		logInfo("chicken", "manually opening door")
        sendCommand(RELAY_1, ON)
        sendCommand(RELAY_2, ON)
    }
end

rule SunriseAction
when
	Item SUNRISE_EVENT received update ON
then
	logInfo("chicken", "received sunrise event")
	if(OPS_MODE.state == 1) {
        sendCommand(RELAY_1, ON)
        sendCommand(RELAY_2, ON)
	} 
end

rule SunsetAction
when
	Item SUNSET_EVENT received update ON
then
	logInfo("chicken", "received sunset event")
	if(OPS_MODE.state == 1) {
        sendCommand(RELAY_1, OFF)
        sendCommand(RELAY_2, OFF)
	}
end

